# Maintainer: seth <getchoo at tuta dot io>

pkgname=hiccup-git
pkgver=r11.a615f6c
pkgrel=1
pkgdesc="a python script to help keep you up to date"
arch=(any)
url="https://github.com/getchoo/hiccup"
license=('MIT')
depends=('xdg-user-dirs')
makedepends=('git' 'python-build' 'python-installer' 
             'python-wheel' 'python-setuptools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+$url.git#branch=main")
sha256sums=('SKIP')


pkgver() {
  cd "$srcdir/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${pkgname%-git}"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/${pkgname%-git}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# nvim: ts=2 sw=2 et:
